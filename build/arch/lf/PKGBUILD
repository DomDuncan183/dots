pkgname=lf
pkgver=32
pkgrel=1
pkgdesc="Terminal file manager"
arch=("x86_64")
url="https://github.com/gokcehan/lf"
license=("MIT")
depends=("glibc")
makedepends=("git" "go")
options=("!debug")
source=("https://github.com/gokcehan/$pkgname/archive/refs/tags/r$pkgver.tar.gz")
sha256sums=("01531e7a78d8bfbe14739072e93446d003f0e4ce12032a26671fa326b73bc911")

build() {
    cd "$pkgname-r$pkgver"
    go mod download
    go build -ldflags "-s -w"
}

check() {
    cd "$pkgname-r$pkgver"
    go test -v ./...
}

package() {
    cd "$pkgname-r$pkgver"
    install -Dm755 -t "$pkgdir/usr/bin" ./lf

    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" ./LICENSE
    install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" ./README.md ./etc/lfrc.example
    install -Dm644 -t "$pkgdir/usr/share/$pkgname" ./etc/lfcd.sh
    install -Dm644 -t "$pkgdir/usr/share/man/man1" ./lf.1

    # vim
    install -Dm644 -t "$pkgdir/usr/share/vim/vimfiles/syntax" ./etc/lf.vim
    install -Dm644 -t "$pkgdir/usr/share/vim/vimfiles/ftdetect" ./etc/lf.vim

    # fish
    install -Dm644 -t "$pkgdir/usr/share/fish/vendor_completions.d" ./etc/lf.fish
    install -Dm644 -t "$pkgdir/usr/share/fish/vendor_functions.d" ./etc/lfcd.fish
}
